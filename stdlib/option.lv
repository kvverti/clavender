' The option namespace contains the Option monad which represents
' the possible absence of a value.

@import global using _
@import hof using Constant

' Returns Some(value) if value is defined, None otherwise
(def Option(value)
    => Some(value) ; sys:defined(value)
    => None ; 1
)

' Object representing the presence of a value.
(def Some(a) =>
    (def obj(b)
        => __yes__ ; b = __object__
        => 1 ; b = \len
        => "Some(" ++ str(a) ++ ")" ; b = \str
        => (def(f) => Option(f(a))) ; b = \map\
        => (def(f) => f(a)) ; b = \flatmap\
        => (def(f)
            => \obj ; f(a)
            => None ; 1) ; b = \filter\
        => (def(obj2) => Some({ a, sys:cval(obj2, 0) })) ; b = \zip\
        => (def(id, f) => f(id, a)) ; b = \fold\
    )
)

' Object representing the absence of a value. None is a synonym for
' `Unit`.
def None() => Unit

' Get the value if defined, else the default value.
def i_valueElse(self, => default) => sys:cval(self, 0) else default
